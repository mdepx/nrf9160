MEMORY
{
	flash   (rx)  : ORIGIN = 0x00040000, LENGTH = 1M
	sram0   (rwx) : ORIGIN = 0x20000000, LENGTH = 16K /* boot loader */
	sram1   (rwx) : ORIGIN = 0x20010000, LENGTH = 64K /* bsdlib */
	sram2   (rwx) : ORIGIN = 0x20020000, LENGTH = 64K /* this app */
	sram3   (rwx) : ORIGIN = 0x20030000, LENGTH = 64K /* malloc */
}

ENTRY(__start)
SECTIONS
{
	. = 0x40000;
	.start . : {
		*start.o(.text);
	} > flash

	.text : {
		*(.exception);
		*(.text*);
	} > flash

	.rodata : {
		*(.rodata*);
	} > flash

	/* Ensure _smem is associated with the next section */
	. = .;
	_smem = ABSOLUTE(.);
	.data : {
		_sdata = ABSOLUTE(.);
		*(.data*);
		_edata = ABSOLUTE(.);
	} > sram2 AT > flash

	.bss : {
		_sbss = ABSOLUTE(.);
		*(.bss*)
		*(COMMON)
		_ebss = ABSOLUTE(.);
	} > sram2
}
