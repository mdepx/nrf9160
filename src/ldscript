MEMORY
{
	/*
	 * top 0x4000 bytes is reserved for the DTB
	 */
	flash   (rx)  : ORIGIN = 0x00040000, LENGTH = 1M - 0x40000 - 0x4000
	sram0   (rwx) : ORIGIN = 0x20000000, LENGTH = 16K /* boot loader */
	sram1   (rwx) : ORIGIN = 0x20010000, LENGTH = 64K /* nrf_modem */
	sram2   (rwx) : ORIGIN = 0x20020000, LENGTH = 64K /* this app */
	sram3   (rwx) : ORIGIN = 0x20030000, LENGTH = 64K /* malloc */
}

ENTRY(__start)
SECTIONS
{
	. = 0x40000;
	.start . : {
		*start.o(.text);
	} > flash

	.text : {
		*(.exception);
		*(.text*);
	} > flash

	.rodata : {
		*(.rodata*);
	} > flash

	/* Ensure _smem is associated with the next section */
	. = .;
	_smem = ABSOLUTE(.);
	.data : {
		_sdata = ABSOLUTE(.);
		*(.data*);

		/*
		 * Ensure sysinit is within the data,
		 * so will be relocated from flash to sram.
		 */
		. = ALIGN(4);
		__start_set_sysinit_set = ABSOLUTE(.);
		*(set_sysinit*)
		__stop_set_sysinit_set = ABSOLUTE(.);

		_edata = ABSOLUTE(.);
	} > sram2 AT > flash

	.bss : {
		_sbss = ABSOLUTE(.);
		*(.bss*)
		*(COMMON)
		_ebss = ABSOLUTE(.);
	} > sram2
}
